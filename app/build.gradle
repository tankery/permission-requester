apply plugin: 'com.android.application'

// Define some common closures (methods) for code share
ext.getTagCount = {
    String cmd = "git tag -l \"v_*\""
    String result = cmd.execute((String[])null, project.projectDir).text
    return result.trim().split("\n").length
}
ext.getLatestTaggedVersion = {
    String cmd = "git describe --abbrev=0 --tags"
    String result = cmd.execute((String[])null, project.projectDir).text
    return result.trim().replace("v_", "")
}

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "me.tankery.permission"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode getTagCount()
        versionName getLatestTaggedVersion()
    }

    buildTypes {
        release {
            signingConfig signingConfigs.debug

            setProperty("archivesBaseName", "permission-requester-demo-${defaultConfig.versionName}")
        }
    }
}

dependencies {
    implementation project(':permission')

    implementation deps.support.appcompat
    implementation deps.support.constraintLayout
    implementation deps.support.design
    implementation deps.timber

    debugImplementation deps.leakcanaryDebug
    releaseImplementation deps.leakcanaryRelease
}
